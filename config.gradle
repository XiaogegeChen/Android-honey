project.ext {

    //设置App配置
    setAppDefaultConfig = {
        extension->
            extension.apply plugin: 'com.android.application'
            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-android-extensions'
            extension.apply plugin: 'kotlin-kapt'
            extension.description "app"

            setAppAndroidConfig extension.android
            setDependencies extension.dependencies
    }

    //设置Lib配置
    setLibDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.library'
            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-android-extensions'
            extension.apply plugin: 'kotlin-kapt'
            extension.description "lib"

            kapt {
                arguments {
                    arg("AROUTER_MODULE_NAME", project.getName())
                }
            }

            setLibAndroidConfig extension.android
            setDependencies extension.dependencies
    }

    //设置App的Android配置
    setAppAndroidConfig ={
        extension->
            extension.compileSdkVersion 28
            extension.defaultConfig {
                applicationId applicationId + "." + extension.project.getName()
                targetSdkVersion 28
                minSdkVersion 21
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [AROUTER_MODULE_NAME: project.getName(), AROUTER_GENERATE_DOC: "enable"]
                    }
                }
            }
            extension.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            extension.sourceSets {
                main {
                    manifest.srcFile 'src/main/debug/AndroidManifest.xml'
                }
            }
    }

    //设置lib的Android配置
    setLibAndroidConfig ={
        extension->
            extension.compileSdkVersion 28
            extension.defaultConfig {
                targetSdkVersion 28
                minSdkVersion 21
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
//                javaCompileOptions {
//                    annotationProcessorOptions {
//                        arguments = [AROUTER_MODULE_NAME: extension.project.getName(), AROUTER_GENERATE_DOC: "enable"]
//                    }
//                }
            }

            extension.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            extension.sourceSets {
                main {
                    manifest.srcFile 'src/main/AndroidManifest.xml'
                    java {
                        exclude 'debug/**'
                    }
                }
            }
    }

    //设置依赖
    setDependencies = {
        extension->
            extension.implementation ('com.alibaba:arouter-api:1.4.1') {
                exclude group: 'com.android.support'
            }
            extension.kapt ('com.alibaba:arouter-compiler:1.2.2')

            extension.implementation fileTree(dir: 'libs', include: ['*.jar'])

            extension.implementation 'androidx.appcompat:appcompat:1.0.2'
            extension.implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
            extension.implementation 'androidx.core:core-ktx:1.0.2'

            extension.implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
            extension.testImplementation 'junit:junit:4.12'
            extension.androidTestImplementation 'androidx.test:runner:1.1.1'
            extension.androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    }
}